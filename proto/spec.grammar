token
	.	= /[ \t]+/			# skip spaces
	.	= /#[^\n]*/			# skip comments

	number	= /0|[1-9][0-9]*/
	name	= /[a-z](-?[0-9a-z])*/
	string	= /"([^"\\]|(\\["\\]))*"/
	eol	= /\n+/				# and skip empty lines

	colon	= ":"
	open	= "{"
	close	= "}"
	eq	= "="
	star	= "*"

grammar
	spec	= childs

	group	= "group" name             ":" string ["{" eol childs "}"] eol
	nodes	= "nodes" name             ":" string ["{" eol nspec  "}"] eol
	node	= "node"  name ["=" const] ":" string ["{" eol nspec  "}"] eol
	attrs	= "attrs" name             ":" string ["{" eol limit  "}"] eol
	attr	= "attr"  name ["=" const] ":" string ["{" eol limit  "}"] eol

	const	= name | number | string

	childs  = (group | nodes | node | attrs | attr)*
	nspec	= limit class* childs
	limit	= value* type* ref?

	value	= "value" const ":" string eol
	type	= "type" "is" name (name const)* eol
	ref	= "ref" (name | "*")+ eol
	class	= "class" name eol

